plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'de.mannodermaus.android-junit5'

    id 'maven-publish'
    id 'signing'
}

def libraryGroup    = 'io.github.neboskreb'
def libraryArtifact = 'instant-task-executor-extension'
def libraryVersion  = '1.0.0'

android {
    kotlinOptions {
        jvmTarget = '1.8'
    }


    defaultConfig {
        compileSdk 34
        targetSdkVersion 34
        minSdkVersion 10
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    namespace = 'com.github.neboskreb.instant.task.executor'

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    repositories {
        maven {
            name "GitHub"
            url = uri("https://maven.pkg.github.com/neboskreb/instant-task-executor-extension")
            credentials {
                username = project.findProperty("github_packages_user") ?: System.getenv("PACKAGES_USERNAME")
                password = project.findProperty("github_packages_token") ?: System.getenv("PACKAGES_TOKEN")
            }
        }
        maven {
            name "MavenCentral"
            if (version.endsWith('SNAPSHOT')) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            } else  {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            }
            credentials {
                username = project.findProperty("mavenCentralUser") ?: System.getenv("MAVEN_USERNAME")
                password = project.findProperty("mavenCentralPass") ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        release(MavenPublication) {
            group = libraryGroup
            artifactId = libraryArtifact
            version = libraryVersion

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'InstantTaskExecutorRule extension'
                description = 'Support for InstantTaskExecutorRule for JUnit 5'
                url = 'https://github.com/neboskreb/instant-task-executor-extension'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'JBM'
                        name = 'John Y. Pazekha'
                        email = 'neboskreb@narod.ru'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:neboskreb/instant-task-executor-extension.git'
                    developerConnection = 'scm:git:ssh://github.com:neboskreb/instant-task-executor-extension.git'
                    url = 'https://github.com/neboskreb/instant-task-executor-extension'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}


java {
    sourceCompatibility = 11
    targetCompatibility = 11
}


dependencies {
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.arch.core:core-testing:2.2.0'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'
    runtimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.11.0'


    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'
    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.11.0'
}
